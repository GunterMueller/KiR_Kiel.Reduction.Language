============================================
Die Ticket-Initialisierungsdatei red.tickets
============================================

Grammatik der Datei
-------------------

<ticket_file>     ::= <dim_specs>'.'
<dim_specs>       ::= <dim_spec> | <dim_spec><dim_specs>
<dim_spec>        ::= <dim_id><proc_specs>
<dim_id>          ::= '*'<number>NL
<proc_specs>      ::= <proc_spec> | <proc_spec><proc_specs>
<proc_spec>       ::= '@'<proc_list>'('<ticket_list>')'<start_proc_list>NL
<start_proc_list> ::= ';'<number> | ';'<number><start_proc_list>
<proc_list>       ::= <proc_range> | <proc_range>,<proc_list>
<proc_range>      ::= <number> | <number>'-'<number>
<ticket_list>     ::= <ticket_spec> | <ticket_spec>;<ticket_list>
<ticket_spec>     ::= <number> | <proc_list>:<number>
<number>          ::= <digit> | <digit><number>

Eine Beispieldatei
------------------

1   *1
2   @0(1:4);1
3   @1(3);0
4   *2
5   @0-2(3);1;2;3
6   @3(0)
7   .

Erlaeuterungen:
----------------

1   Hier beginnt die Belegung fuer einen nCube der Dimension 1
2   Prozessor 0 bekommt 4 Tickets fuer Prozessor 1
    und verteilt zuerst an Prozessor 1
3   Prozessor 1 bekommt fuer alle direkten Nachbarn 3 Tickets
    und verteilt zuerst an Prozessor 0
4   Hier beginnt die Belegung fuer einen nCube der Dimension 2
5   Die Prozessoren 0,1 und 2 bekommen jeweils fuer ihre direkten
    Nachbarn 3 Tickets, 0 verteilt zuerst an 1, 1 an 2 und 2 an 3
6   Prozessor 3 bekommt fuer alle direkten Nachbarn 0 Tickets, kann
    also ueberhaupt nicht verteilen.
7   Hier endet die Datei

Anmerkungen:
------------

- Ist fuer die Groesse eines Subcubes keine Initialisierung vorgegeben,
  wird die Standardbelegung benutzt, d.h. genau ein Ticket pro
  (Hypercube-)Nachbar.

- Ist fuer einen Prozessor keine Initialisierung vorgegeben, wird die
  Standardbelegung benutzt.
